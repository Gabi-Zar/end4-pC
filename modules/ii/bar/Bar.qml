pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import Quickshell.Hyprland
import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets

Scope {
    id: bar

    Variants {
        // For each monitor
        model: {
            const screens = Quickshell.screens;
            // Evaluated before any per-screen window exists, so this is always the
            // global default - "which screens show a bar" isn't itself per-screen.
            const list = Config.options.bar.screenList;
            const base = (!list || list.length === 0) ? screens : screens.filter(screen => list.includes(screen.name));
            // Each screen's own orientation setting decides which of Bar/VerticalBar it gets
            return base.filter(screen => !Config.barOption(screen.name, "vertical"));
        }
        LazyLoader {
            id: barLoader
            active: GlobalStates.barOpen && !GlobalStates.screenLocked
            required property ShellScreen modelData
            component: PanelWindow { // Bar window
                id: barRoot
                screen: barLoader.modelData
                readonly property string __barScreen: barRoot.screen?.name ?? ""
                property bool showBarBackground: Config.barOption(barRoot.__barScreen, "showBackground")

                Timer {
                    id: showBarTimer
                    interval: (Config.barOption(barRoot.__barScreen, "autoHide.showWhenPressingSuper.delay") ?? 100)
                    repeat: false
                    onTriggered: {
                        barRoot.superShow = true
                    }
                }
                Connections {
                    target: GlobalStates
                    function onSuperDownChanged() {
                        if (!Config.barOption(barRoot.__barScreen, "autoHide.showWhenPressingSuper.enable")) return;
                        if (GlobalStates.superDown) showBarTimer.restart();
                        else {
                            showBarTimer.stop();
                            barRoot.superShow = false;
                        }
                    }
                }

                property bool showCorners: !Config.barOption(barRoot.__barScreen, "autoHide.enable") || mustShow

                Timer {
                    id: cornerRevealTimer
                    interval: 65
                    onTriggered: barRoot.showCorners = true
                }

                onMustShowChanged: {
                    if (!Config.barOption(barRoot.__barScreen, "autoHide.enable")) return;
                    if (mustShow) {
                        cornerRevealTimer.restart()
                    } else {
                        cornerRevealTimer.stop()
                        barRoot.showCorners = false
                    }
                }
                property bool superShow: false
                property bool mustShow: hoverRegion.containsMouse || superShow
                property var thisMonitorData: HyprlandData.monitors.find(m => m.name === barRoot.screen?.name)
                property bool monitorHasFullscreen: HyprlandData.workspaceById[thisMonitorData?.activeWorkspace?.id]?.hasfullscreen ?? false
                property bool monitorHasSpecialOpen: (thisMonitorData?.specialWorkspace?.name ?? "") !== ""
                exclusionMode: ExclusionMode.Ignore
                property int normalExclusiveZone: (Config.barOption(barRoot.__barScreen, "autoHide.enable") && (!mustShow || !Config.barOption(barRoot.__barScreen, "autoHide.pushWindows")))
                    ? 0
                    : Appearance.sizes.baseBarHeight
                        + (Config.barOption(barRoot.__barScreen, "cornerStyle") === 1 ? Appearance.sizes.hyprlandGapsOut : 0)
                        + (Config.barOption(barRoot.__barScreen, "cornerStyle") === 2 ? -6 : 0)

                exclusiveZone: (barContent.centerOnly && Config.barOption(barRoot.__barScreen, "centerOnlyReserveFrame"))
                    ? Config.barOption(barRoot.__barScreen, "frameThickness")
                    : Config.barOption(barRoot.__barScreen, "cornerStyle") === 4 ? normalExclusiveZone + 4 : normalExclusiveZone
                WlrLayershell.namespace: "quickshell:bar"
                // Overlay layer only while special workspace sits on top of a fullscreen window on this monitor,
                // else Top layer so fullscreen apps cover the bar as normal (Hyprland buries Top layer under fullscreen+special).
                WlrLayershell.layer: (monitorHasFullscreen && monitorHasSpecialOpen) ? WlrLayer.Overlay : WlrLayer.Top
                implicitHeight: Appearance.sizes.barHeight + Appearance.rounding.screenRounding
                // When Overlay-layer, bar shares a layer with the screen-corner click zones (ScreenCorners.qml)
                // and same-layer overlap is resolved by stacking, not layer priority - bar was winning and
                // swallowing the tiny corner-open hit rects. Carve them out of the bar's own mask so clicks
                // reach the corners underneath. Only relevant on the edge the bar and corners share.
                property bool cutOutCornerOpenZones: (monitorHasFullscreen && monitorHasSpecialOpen) && (Config.barOption(barRoot.__barScreen, "bottom") === Config.options.sidebar.cornerOpen.bottom)
                property int cornerOpenCutWidth: cutOutCornerOpenZones ? Config.options.sidebar.cornerOpen.cornerRegionWidth : 0
                property int cornerOpenCutHeight: cutOutCornerOpenZones ? Config.options.sidebar.cornerOpen.cornerRegionHeight : 0
                mask: Region {
                    item: hoverMaskRegion
                    Region {
                        intersection: Intersection.Subtract
                        x: 0
                        y: Config.barOption(barRoot.__barScreen, "bottom") ? (barRoot.height - barRoot.cornerOpenCutHeight) : 0
                        width: barRoot.cornerOpenCutWidth
                        height: barRoot.cornerOpenCutHeight
                    }
                    Region {
                        intersection: Intersection.Subtract
                        x: barRoot.width - barRoot.cornerOpenCutWidth
                        y: Config.barOption(barRoot.__barScreen, "bottom") ? (barRoot.height - barRoot.cornerOpenCutHeight) : 0
                        width: barRoot.cornerOpenCutWidth
                        height: barRoot.cornerOpenCutHeight
                    }
                }
                color: "transparent"

                // Positioning
                anchors {
                    top: !Config.barOption(barRoot.__barScreen, "bottom")
                    bottom: Config.barOption(barRoot.__barScreen, "bottom")
                    left: true
                    right: true
                }

                margins {
                    top: Config.barOption(barRoot.__barScreen, "cornerStyle") === 3 ? 5 : 0
                    right: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.right) * -1
                    bottom: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.bottom) * -1 || Config.barOption(barRoot.__barScreen, "cornerStyle") === 3 ? 5 : 0
                }

                // Include in focus grab
                Component.onCompleted: {
                    GlobalFocusGrab.addPersistent(barRoot);
                }
                Component.onDestruction: {
                    GlobalFocusGrab.removePersistent(barRoot);
                }

                MouseArea  {
                    id: hoverRegion
                    hoverEnabled: true
                    anchors {
                        fill: parent
                        rightMargin: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.right) * 1
                        bottomMargin: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.bottom) * 1
                    }

                    Item {
                        id: hoverMaskRegion
                        anchors {
                            fill: barContent
                            topMargin: -Config.barOption(barRoot.__barScreen, "autoHide.hoverRegionWidth")
                            bottomMargin: -Config.barOption(barRoot.__barScreen, "autoHide.hoverRegionWidth")
                        }
                    }

                    BarContent {
                        id: barContent
                        screenName: barRoot.__barScreen
                        
                        implicitHeight: Appearance.sizes.barHeight
                        anchors {
                            right: parent.right
                            left: parent.left
                            top: parent.top
                            bottom: undefined
                            topMargin: (Config.barOption(barRoot.__barScreen, "autoHide.enable") && !mustShow) ? -Appearance.sizes.barHeight : 0
                            bottomMargin: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.bottom) * -1
                            rightMargin: (Config.options.interactions.deadPixelWorkaround.enable && barRoot.anchors.right) * -1
                        }
                        Behavior on anchors.topMargin {
                            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                        }
                        Behavior on anchors.bottomMargin {
                            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                        }

                        states: State {
                            name: "bottom"
                            when: Config.barOption(barRoot.__barScreen, "bottom")
                            AnchorChanges {
                                target: barContent
                                anchors {
                                    right: parent.right
                                    left: parent.left
                                    top: undefined
                                    bottom: parent.bottom
                                }
                            }
                            PropertyChanges {
                                target: barContent
                                anchors.topMargin: 0
                                anchors.bottomMargin: (Config.barOption(barRoot.__barScreen, "autoHide.enable") && !mustShow) ? -Appearance.sizes.barHeight : 0
                            }
                        }
                    }
                    
                    // Round decorators
                    Loader {
                        id: roundDecorators
                        anchors {
                            left: parent.left
                            right: parent.right
                            top: barContent.bottom
                            bottom: undefined
                        }
                        height: Appearance.rounding.screenRounding
                        active: showBarBackground && Config.barOption(barRoot.__barScreen, "cornerStyle") === 0 && !barContent.centerOnly// Hug

                        states: State {
                            name: "bottom"
                            when: Config.barOption(barRoot.__barScreen, "bottom")
                            AnchorChanges {
                                target: roundDecorators
                                anchors {
                                    right: parent.right
                                    left: parent.left
                                    top: undefined
                                    bottom: barContent.top
                                }
                            }
                        }

                        sourceComponent: Item {
                            implicitHeight: Appearance.rounding.screenRounding

                            readonly property color decoratorColor: showBarBackground
                                ? (Config.barOption(barRoot.__barScreen, "followFrameColor") && Config.barOption(barRoot.__barScreen, "frameColor")
                                    ? Appearance.getColorFromName(Config.barOption(barRoot.__barScreen, "frameColor"))
                                    : Appearance.colors.colLayer0)
                                : "transparent"

                            RoundCorner {
                                id: leftCorner
                                anchors {
                                    top: parent.top
                                    bottom: parent.bottom
                                    left: parent.left
                                }

                                implicitSize: Appearance.rounding.screenRounding
                                color: parent.decoratorColor

                                corner: RoundCorner.CornerEnum.TopLeft
                                states: State {
                                    name: "bottom"
                                    when: Config.barOption(barRoot.__barScreen, "bottom")
                                    PropertyChanges {
                                        leftCorner.corner: RoundCorner.CornerEnum.BottomLeft
                                    }
                                }
                            }
                            RoundCorner {
                                id: rightCorner
                                anchors {
                                    right: parent.right
                                    top: !Config.barOption(barRoot.__barScreen, "bottom") ? parent.top : undefined
                                    bottom: Config.barOption(barRoot.__barScreen, "bottom") ? parent.bottom : undefined
                                }
                                implicitSize: Appearance.rounding.screenRounding
                                color: parent.decoratorColor

                                corner: RoundCorner.CornerEnum.TopRight
                                states: State {
                                    name: "bottom"
                                    when: Config.barOption(barRoot.__barScreen, "bottom")
                                    PropertyChanges {
                                        rightCorner.corner: RoundCorner.CornerEnum.BottomRight
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    IpcHandler {
        target: "bar"

        function toggle(): void {
            GlobalStates.barOpen = !GlobalStates.barOpen
        }

        function close(): void {
            GlobalStates.barOpen = false
        }

        function open(): void {
            GlobalStates.barOpen = true
        }
    }

    CompositorGlobalShortcut {
        name: "barToggle"
        description: "Toggles bar on press"

        onPressed: {
            GlobalStates.barOpen = !GlobalStates.barOpen;
        }
    }

    CompositorGlobalShortcut {
        name: "barOpen"
        description: "Opens bar on press"

        onPressed: {
            GlobalStates.barOpen = true;
        }
    }

    CompositorGlobalShortcut {
        name: "barClose"
        description: "Closes bar on press"

        onPressed: {
            GlobalStates.barOpen = false;
        }
    }
}
